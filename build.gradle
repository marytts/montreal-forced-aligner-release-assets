buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '3.3.0'
    }
}

plugins {
    id 'ivy-publish'
}

allprojects {
    apply plugin: 'base'
}

evaluationDependsOnChildren()

subprojects {
    task downloadMFA(type: de.undercouch.gradle.tasks.download.Download) {
        def url = "https://github.com/MontrealCorpusTools/Montreal-Forced-Aligner/releases/download/v$version/montreal-forced-aligner_${project.name}.$extension"
        src url
        dest buildDir
        overwrite false
    }

    task unpackMFA(type: Unpack) {
        dependsOn downloadMFA
        srcFile = file("$buildDir/montreal-forced-aligner_${project.name}.$extension")
        destDir = file("$buildDir/unpacked")
    }
}

publishing {
    publications {
        subprojects.each { subproject ->
            ivy(IvyPublication) {
                artifact("$subproject.buildDir/montreal-forced-aligner_${subproject.name}.$subproject.extension") {
                    extension subproject.extension
                    type subproject.extension
                    classifier subproject.name
                    builtBy subproject.downloadMFA
                    descriptor.status 'release'
                }
            }
        }
    }
    repositories {
        ivy {
            url "$buildDir/repo"
        }
    }
}

class Unpack extends DefaultTask {

    @InputFile
    final RegularFileProperty srcFile = newInputFile()

    @OutputDirectory
    final DirectoryProperty destDir = newOutputDirectory()

    @TaskAction
    void unpack() {
        def src = srcFile.get().asFile
        switch (src.name) {
            case { it.endsWith('.zip') }:
                project.copy {
                    from project.zipTree(src)
                    into destDir
                }
                break
            case { it.endsWith('.tar.gz') }:
                project.copy {
                    from project.tarTree(src)
                    into destDir
                }
                break
            default:
                project.logger.error "Cannot unpack file: $src"
                break
        }
    }
}
